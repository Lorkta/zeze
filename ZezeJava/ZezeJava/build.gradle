apply from: '../build.gradle'

dependencies {
    // required dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.jetbrains:annotations:23.1.0'
    implementation 'org.pcollections:pcollections:4.0.1'
    implementation 'org.rocksdb:rocksdbjni:7.7.3'
    implementation 'io.netty:netty-codec-http:4.1.85.Final'
    implementation 'io.netty:netty-transport-native-epoll:4.1.85.Final:linux-x86_64'
    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1'

    // optional dependencies
    // for tikv
    compileOnly 'org.tikv:tikv-client-java:3.3.0'
    // runtimeOnly 'org.slf4j:slf4j-api:2.0.5'
    // runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'

    // for jdbc(mysql,sqlserver,tidb)
    compileOnly 'com.alibaba:druid:1.2.15'
    compileOnly 'mysql:mysql-connector-java:8.0.31'

    // dynamodb
    compileOnly 'com.amazonaws:aws-java-sdk-dynamodb:1.12.342'

    // for rocketmq
    compileOnly 'org.apache.rocketmq:rocketmq-client:4.9.4'
    compileOnly 'javax:javaee-api:8.0.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--add-exports", "java.base/sun.security.rsa=ALL-UNNAMED",
                             "--add-exports", "java.base/sun.security.util=ALL-UNNAMED",
                             "--add-exports", "java.base/sun.security.x509=ALL-UNNAMED"]
}

task startServiceManager(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.ServiceManagerServer'
    allJvmArgs = ['-Dlogname=ServiceManagerServer']
}

task startGlobalCacheManager(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.GlobalCacheManagerServer'
    allJvmArgs = ['-Dlogname=GlobalCacheManagerServer']
}

task startGlobalCacheManagerAsync(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.GlobalCacheManagerAsyncServer'
    allJvmArgs = ['-Dlogname=GlobalCacheManagerAsyncServer']
}
