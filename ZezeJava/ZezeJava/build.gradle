apply from: '../build.gradle'

dependencies {
    // required dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'org.pcollections:pcollections:4.0.1'
    implementation 'org.rocksdb:rocksdbjni:8.10.0'

    // for netty
    implementation 'io.netty:netty-codec-http:4.1.105.Final'
    implementation 'io.netty:netty-transport-native-epoll:4.1.105.Final:linux-x86_64'
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.22.1'

    // optional dependencies
    // for tikv
    compileOnly 'org.tikv:tikv-client-java:3.3.5'
    // runtimeOnly 'org.slf4j:slf4j-api:1.7.36'
    // runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.22.1'

    // for jdbc(mysql,sqlserver,tidb)
    compileOnly 'com.alibaba:druid:1.2.21'
    compileOnly 'com.mysql:mysql-connector-j:8.3.0'

    // dynamodb
    compileOnly 'com.amazonaws:aws-java-sdk-dynamodb:1.12.568'

    // foundationDB
    compileOnly 'org.foundationdb:fdb-java:7.3.33'

    // for rocketmq
    compileOnly 'org.apache.rocketmq:rocketmq-client:4.9.7'
    compileOnly 'javax:javaee-api:8.0.1'

    // for task component
    compileOnly 'org.jgrapht:jgrapht-core:1.5.2'
    compileOnly 'com.opencsv:opencsv:5.9'

    // for new Compress/Decompress Codec in Zeze.Net
    compileOnly 'com.github.luben:zstd-jni:1.5.5-6'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--add-exports", "java.base/sun.security.rsa=ALL-UNNAMED",
                             "--add-exports", "java.base/sun.security.util=ALL-UNNAMED",
                             "--add-exports", "java.base/sun.security.x509=ALL-UNNAMED"]
}

task startServiceManager(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.ServiceManagerServer'
    allJvmArgs = ['-Dlogname=ServiceManagerServer']
}

task startGlobalCacheManager(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.GlobalCacheManagerServer'
    allJvmArgs = ['-Dlogname=GlobalCacheManagerServer']
}

task startGlobalCacheManagerAsync(type: JavaExec) {
    classpath = configurations.runtimeClasspath
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('.')
    mainClass = 'Zeze.Services.GlobalCacheManagerAsyncServer'
    allJvmArgs = ['-Dlogname=GlobalCacheManagerAsyncServer']
}
